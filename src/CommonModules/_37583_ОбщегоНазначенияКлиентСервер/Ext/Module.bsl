
#Область ПрограммныйИнтерфейс_ОбщегоИспользования

Функция СтрокаСимволов(Символ = " ", Число) Экспорт
    Возврат СтрЗаменить(Лев(СтрЗаменить(Формат(1, "ЧВН=; ЧГ=0; ЧЦ=" + Число), "1", "0"), Число), "0",Символ);
КонецФункции

Функция ШтампВремени() Экспорт
    Возврат СтрЗаменить(СтрЗаменить(СтрЗаменить(Строка(ТекущаяДата()),".",""),":",""),Символ(32),"");
КонецФункции 

Функция НормализоватьТекст(Текст,МеткаEndOfText = Истина,СтрокаСлов ="")  Экспорт
    КодАлгоритма = СтрЗаменить(Текст,Символы.Таб," ");
	КодАлгоритма = СтрЗаменить(КодАлгоритма,Символы.ВК,"");
	КодАлгоритма = СтрЗаменить(КодАлгоритма,Символы.ПС," ^ ");
    Для А = 0 по Окр(Sqrt(СтрЧислоВхождений(КодАлгоритма,"  ")),0) Цикл
        КодАлгоритма = СтрЗаменить(КодАлгоритма,"  "," ");
    КонецЦикла;
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"< =","<=");
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"> =",">=");
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"< >","<>");
	КодАлгоритма = СтрЗаменить(КодАлгоритма," =","=");
	КодАлгоритма = СтрЗаменить(КодАлгоритма,"= ","=");
    МассивСлов =  СтрРазделить(КодАлгоритма," ");
    СтрокаСлужебныхСлов ="Возврат,И,Или,Не,Если,Тогда,КонецЕсли,Для,Каждого,Из,Пока,Цикл,КонецЦикла";
    МассивСлужебныхСлов = СтрРазделить(?(ПустаяСтрока(СтрокаСлов),СтрокаСлужебныхСлов,СтрокаСлов), ",") ;
    Для  Слово = 0  По МассивСлов.ВГраница() Цикл
        Для  Каждого СлужебноеСлово Из МассивСлужебныхСлов Цикл
            Если НРег(СокрЛП(МассивСлов[Слово])) = НРег(СлужебноеСлово) Тогда 
               МассивСлов[Слово]  = СлужебноеСлово;   
            КонецЕсли;
        КонецЦикла;
    КонецЦикла;
    КодАлгоритма = СтрСоединить(МассивСлов," ");
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"^", Символы.ПС);
    Если МеткаEndOfText  И СтрНайти(КодАлгоритма,"~EndOfText:") = 0 Тогда
        КодАлгоритма = КодАлгоритма+Символы.ПС+"~EndOfText:";	
    КонецЕсли;
    Возврат КодАлгоритма;
КонецФункции

Процедура пЗаменаВставка(КодАлг,Префикс,До="",После="",Исключая="",Включая ="") Экспорт
    Пока СтрНайти(КодАлг,Префикс)>0 Цикл
        Слово = ПолучитьПервоеВхождениеСловоБезПрефикса(КодАлг,Префикс,Исключая,Включая);
        КодАлг = СтрЗаменить(КодАлг,Префикс+Слово,До+Слово+После);
    КонецЦикла;
КонецПроцедуры

Функция фЗаменаВставка(Знач КодАлг,Префикс,До="",После="",Исключая="",Включая ="") Экспорт
    Пока СтрНайти(КодАлг,Префикс)>0 Цикл
        Слово = ПолучитьПервоеВхождениеСловоБезПрефикса(КодАлг,Префикс,Исключая,Включая);
        КодАлг = СтрЗаменить(КодАлг,Префикс+Слово,До+Слово+После);
    КонецЦикла;
    Возврат КодАлг;
КонецФункции

Функция ПолучитьПервоеВхождениеСловоБезПрефикса(Строка,Преф,Исключая = ";,+, = ,-,),(,.,[,],{,},|,/,\,>,<",Включая ="") Экспорт
    ДлинаПреф = СтрДлина(Преф);
    ПозПрефикс = СтрНайти(Строка,Преф)+ДлинаПреф;
    
    Если ТипЗнч(Включая) =Тип("Массив") Тогда
        мВключая = Включая;
    Иначе
        мВключая = СтрРазделить(Включая,",");
    КонецЕсли;
    
    Если ТипЗнч(Исключая) =Тип("Массив") Тогда
        мИсключая = Исключая;
    Иначе
        мИсключая = СтрРазделить(Исключая,",");
    КонецЕсли;

    мТерм = Новый Массив;
    Для Каждого СимволИсключая Из мИсключая Цикл
        Если мВключая.Найти(СимволИсключая) = Неопределено Тогда 
            мТерм.Добавить(СтрНайти(Сред(Строка, ПозПрефикс),СимволИсключая));
        КонецЕсли;
    КонецЦикла;
    
    Терм = 1000000 ;
    Для каждого Элемент Из мТерм Цикл
        Если Элемент > 0 И Элемент < Терм Тогда 
            Терм = Элемент;
        КонецЕсли;
    КонецЦикла;
    
    Слово = ?(Терм < 1000000, Сред(Строка, СтрНайти(Строка,Преф) + ДлинаПреф, Терм - 1),Сред(Строка, СтрНайти(Строка,Преф) + ДлинаПреф));
    Возврат Слово;
КонецФункции

Функция СтрокаВМассив(Строка,Разделитель) Экспорт
    Возврат СтрРазделить(Строка,Разделитель);
КонецФункции 

Функция ПолучитьСиноним(Знач Строка) Экспорт 
    СтрокаСокрЛП = СокрЛП(Строка);
    Синоним = ""; 
    Заглавные = "АБВГДЕЁЖЗИКЛМНОПРСТФХЦЧЩЭЮЯABCDIFGHIJKLMNOPQRSTUVWXYZ";
    До = Истина;
    Для Ё = 1 По СтрДлина(СтрокаСокрЛП) Цикл 
        Символ = Сред(СтрокаСокрЛП,Ё,1); 
        После = Ё < СтрДлина(СтрокаСокрЛП) И СтрНайти(Заглавные, Сред(СтрокаСокрЛП,Ё + 1, 1))> 0; 
        Если СтрНайти(Заглавные,Символ) > 0 Тогда 
            Синоним = Синоним + ?(До, "", Символ(32)) + ?(До ИЛИ После, Символ, НРег(Символ));
            До = Истина ;
        Иначе 
            Синоним = Синоним + Символ;
            До = Ложь;
        КонецЕсли; 
    КонецЦикла;
    Возврат Синоним; 
КонецФункции // ПолучитьСиноним()

  // получить верное имя переменной из строки
  // почему так сделано ?? забыл 
Функция СклеитьСтроку(Знач Строка) Экспорт
    Строка = СтрЗаменить(СтрЗаменить(СтрЗаменить(СокрЛП(Строка),Символы.Таб,""),Символы.ПС,""),Символ(32),""); 
    СтрИсключая = "0123456789,+=-)(.:[]{}|/\><$@#`""~*^%№?"; 
    Курсор = СтрДлина(Строка);
    СтрокаВ = "";
      Пока Курсор > 0 Цикл
        Если СтрНайти(СтрИсключая,Сред(Строка,Курсор,1)) = 0 Тогда
            СтрокаВ = Сред(Строка,Курсор,1) + СтрокаВ;
        КонецЕсли; 
        Курсор = Курсор -1; 
    КонецЦикла;
    Возврат СтрокаВ; 
КонецФункции // ПолучитьСиноним()

Функция ЭтоLinux()   Экспорт
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Возврат  СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64 ИЛИ СистемнаяИнформация.ТипПлатформы =ТипПлатформы.Linux_x86;
КонецФункции

//Вставка строк текста  в многострочный текст

Функция СтрокиСлева(Текст,УникальныйТекст) Экспорт
    НачалоУникТекста = СтрНайти(Текст,УникальныйТекст);
    ТекстСлева =  Лев(Текст,НачалоУникТекста-1);
    ПереносСлева = Мин(НачалоУникТекста-1,СтрНайти(ТекстСлева,Символы.ПС,НаправлениеПоиска.СКонца));
    Возврат Лев(Текст,ПереносСлева);
КонецФункции

Функция СтрокиСправа(Текст,УникальныйТекст) Экспорт
    КонецУникТекста = СтрНайти(Текст,УникальныйТекст)+СтрДлина(УникальныйТекст)-1;
    ТекстСправа = Сред(Текст,КонецУникТекста+1) ;
    ПереносСправа = СтрНайти(ТекстСправа,Символы.ПС);
    Возврат ?(ПереносСправа=0,ТекстСправа,Сред(ТекстСправа,ПереносСправа));
КонецФункции

Функция ВставитьМежду(Текст,УникальныйТекст,Преф,Суфф) Экспорт 
    Л = СтрокиСлева(Текст,УникальныйТекст); 
    П = СтрокиСправа(Текст,УникальныйТекст); 
    Возврат Л + Преф + УникальныйТекст + Суфф +П;
КонецФункции

Функция Транслитерация(СтрокаТекста) Экспорт
	// """ взято из комментариев к  https://infostart.ru/public/57971/ """
    стрРус = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя ";
    стрEng = "A///B///V///G///D///E///YO//ZH//Z///I///Y///K///L///M///N///O///P///R///S///T///U///F///H///TS//CH//SH//SHCH_///Y///_///E///YU//JA//a///b///v///g///d///e///yo//zh//z///i///y///k///l///m///n///o///p///r///s///t///u///f///h///ts//ch//sh//shch_///y///_///e///yu//ja///_//";
    Результат = "";
    Для Сч = 1 по СтрДлина(СтрокаТекста) Цикл
        Символ = Сред(СтрокаТекста, Сч, 1);
        Позиция = Найти(стрРус, Символ);
        Если Позиция = 0 Тогда
            Результат = Результат + Символ
        Иначе
            Результат = Результат + СтрЗаменить(Сред(стрEng, (Позиция - 1) * 4 + 1, 4), "/", "")
        КонецЕсли
    КонецЦикла;
    Возврат Результат
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс_Алгоритмы

Функция МассивИсключаемыхСимволов() Экспорт
    мИсключая = СтрРазделить(";,+, = ,-,),(,.,[,],{,},|,/,\,>,<,$,@,#",",");
    мИсключая.Добавить(Символы.ПС);
    мИсключая.Добавить(Символы.Таб);
    мИсключая.Добавить(Символ(32));
    мИсключая.Добавить(",");
    мИсключая.Добавить("""");
    Возврат мИсключая;
КонецФункции

Функция ПреобразоватьТекстВКодАлгоритма(Текст) Экспорт 
	
	ПодключитьДекораторыКода(Текст);
	
	КодАлгоритма = НормализоватьТекст(Текст);
	
	мИсключая = МассивИсключаемыхСимволов();
	
	// отображение в коде
	
	// часто используемая конструкция
    КодАлгоритма = СтрЗаменить(СтрЗаменить(КодАлгоритма,"%)",")"),"(%","Новый Структура(");
	
	// выполнение  текущего контекста
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"__local","ВыполнитьЛокальныйКонтекст");
	
	// возвращает результат выполнения функции (соответствие) на клиенте
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"__фк","_37583_АлгоритмыКлиент.ВыполнитьФункцию");
	
	// возвращает результат выполнения функции (соответствие) на сервере
    КодАлгоритма = СтрЗаменить(КодАлгоритма,"__ф","_37583_АлгоритмыСервер.ВыполнитьФункцию");
	
	КодАлгоритма = СтрЗаменить(КодАлгоритма,"__env();","_37583_АлгоритмыКэш.ПолучитьНастройкиПоУмолчанию();");
	
	КодАлгоритма = СтрЗаменить(КодАлгоритма,"__","Параметры");

    пЗаменаВставка(КодАлгоритма,"#","[""","""]",мИсключая);
	
	пЗаменаВставка(КодАлгоритма,"$","this[""","""]",мИсключая);
	
	КодАлгоритма = СтрЗаменить(КодАлгоритма,"Возврат ;","Перейти ~EndOfText;");
	
	пЗаменаВставка(КодАлгоритма,"Возврат ","this[""Результат""] = ","; Перейти ~EndOfText",";"," ");
    Возврат КодАлгоритма;
КонецФункции

Процедура ПодключитьДекораторыКода(Текст)
	МассивДекораторов = Новый Массив;
	Префикс = "//@decorate_";
	Найден = СтрНайти(Текст,Префикс);
	ъ=0; // отладка беск. цикл
	Пока Найден > 0 Цикл
		Начало = СтрНайти(Текст,Символы.ПС,,Найден)+1;
		Декоратор = СокрЛП(Сред(Текст,Найден,Начало-Найден));
		Имя = Лев(Декоратор,СтрНайти(Декоратор,"_",,,2));
		СтрокаУИД = СтрЗаменить(Декоратор,Имя,"");
		Суффикс =СтрНайти(Текст,"//"+СтрокаУИД,,Начало);
		ТекстНаЗамену = СокрЛП(Сред(Текст,Начало,Суффикс-Начало-1));
		Попытка 
			Decorator = Новый Структура("decorate", ТекстНаЗамену); 
			_37583_АлгоритмыСервер.ВыполнитьПроцедуру(Новый УникальныйИдентификатор(СтрокаУИД),Decorator);
			ТекстНаЗамену = Decorator.decorate;
			ПрефиксИзменен = "//@decorated_";
		Исключение
			ПрефиксИзменен = "//@decorated_Error_";
		КонецПопытки;
		Текст = Лев(Текст,Найден-1)+ СтрЗаменить(Декоратор,Префикс,ПрефиксИзменен)+Символы.ПС+ТекстНаЗамену+Символы.ПС+ СокрЛП(Сред(Текст,Суффикс));
		// СтрЗаменить не годится т.к. заменит все вхождения
		ъ = ъ +1;
		Если ъ > 20 Тогда 
			Сообщить("зацикливание, выполнение прервано после 20 итераций");
			Прервать;
		КонецЕсли;	
		Найден = СтрНайти(Текст,Префикс);
	КонецЦикла;
КонецПроцедуры

Функция СвойстваДоступныеНаКлиентеИНаСервере () Экспорт
	СвойстваДоступныеНаКлиентеИНаСервере =  Новый Структура();
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("Ссылка",Неопределено);
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("Наименование","-");
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("КодАлгоритма","");
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("КодЗавершения","");
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("ТолькоТекст",Ложь);
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("ВыбрасыватьИсключение",Истина);
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("ЗаписыватьОшибкиВЖР",Истина);
	СвойстваДоступныеНаКлиентеИНаСервере.Вставить("ЗаписыватьСобытиияВЖР",Ложь);
	Возврат СвойстваДоступныеНаКлиентеИНаСервере;
КонецФункции

#КонецОбласти

