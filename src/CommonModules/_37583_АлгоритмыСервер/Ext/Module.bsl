
#Область ПрограммныйИнтерфейс_Алгоритмы

Процедура ВыполнитьРегламентнуюПроцедуру(Алгоритм) Экспорт
    ВыполнитьПроцедуру(Алгоритм);
КонецПроцедуры

Процедура ВыполнитьПроцедуру(ПредставлениеАлгоритма,ДополнительныеПараметры = Неопределено,ОшибкаВыполнения = Ложь,СообщениеОбОшибке = "") Экспорт
	//""" для совместимости со старыми версиями """
	ВыполнитьФункцию(ПредставлениеАлгоритма,ДополнительныеПараметры,ОшибкаВыполнения,СообщениеОбОшибке);
КонецПроцедуры

Функция ВыполнитьФункцию(ПредставлениеАлгоритма,ДополнительныеПараметры = Неопределено,ОшибкаВыполнения = Ложь,СообщениеОбОшибке = "") Экспорт
	
	Алгоритм = ПолучитьСсылкуСправочникАлгоритмы(ПредставлениеАлгоритма);
	
	Если Алгоритм = Неопределено Или Алгоритм.Пустая() Тогда
		СообщениеОбОшибке = " Ошибка выполнения (не определен сценарий )";
		ОшибкаВыполнения = Истина;
		Если ДополнительныеПараметры = Неопределено Тогда 
			ДополнительныеПараметры = Новый Структура;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("Отказ",Истина);
		ДополнительныеПараметры.Вставить("СообщениеОбОшибке",СообщениеОбОшибке);
		Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер","Алгоритм = Неопределено Или Алгоритм.Пустая()" ,"ВыполнитьФункцию");
	КонецЕсли;
	
	Если Алгоритм.Кэшировать Тогда
		РезультатВыполнения = _37583_АлгоритмыКэш.ВыполнитьФункцию(Алгоритм,ДополнительныеПараметры);
	Иначе
		Объект = Алгоритм.ПолучитьОбъект();
		РезультатВыполнения = Объект.ВыполнитьФункцию(ДополнительныеПараметры);
	КонецЕсли;
	
	СообщениеОбОшибке = ДополнительныеПараметры.СообщениеОбОшибке;
	ОшибкаВыполнения = ДополнительныеПараметры.Отказ;
	
	Возврат  РезультатВыполнения;
	
КонецФункции

Функция ПолучитьНастройкиПоУмолчанию() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_37583_ALG.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник._37583_ALG КАК _37583_ALG
	|ГДЕ
	|	_37583_ALG.ХранилищеНастроек = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если  ВыборкаДетальныеЗаписи.Следующий() Тогда
		Настройки37583 = _37583_АлгоритмыСервер.ПолучитьПараметры(ВыборкаДетальныеЗаписи.Ссылка);
	Иначе
		Настройки37583 = Новый Структура;
	КонецЕсли;
	Возврат Настройки37583;
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс_ОбщегоНазначения

Процедура ЗаписатьВЖурналРегистрации(ИмяСобытия = "",Комментарий = "",СсылкаНаОбъект = Неопределено,ТипСообщения = "Ошибка") Экспорт 
	ИмяСобытия = ?(ИмяСобытия = "","37583_ALG (&НаСервере): ", ИмяСобытия);
    Уровень = ?(ТипСообщения = "Ошибка", УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
    Если СсылкаНаОбъект = Неопределено Тогда
        ЗаписьЖурналаРегистрации(ИмяСобытия,Уровень,,,Комментарий);	
    Иначе 
        ЗаписьЖурналаРегистрации(ИмяСобытия,Уровень,,СсылкаНаОбъект,Комментарий);	
    КонецЕсли;	
КонецПроцедуры

Функция МассивСтрокВСтроку(Массив,Разделитель="") Экспорт
    Если Массив.Количество()>0 Тогда
        Возврат ЗначениеИзСтрокиВнутр(СтрПолучитьСтроку(Стрзаменить(ЗначениеВСтрокуВнутр(Массив),"""},"+Символы.ПС+"{""S"",""",""+Разделитель+""),3));
    Иначе 
        Возврат "";
    КонецЕсли;	
КонецФункции 

Функция СтрокаВМассив(Строка,Разделитель) Экспорт
    Возврат ЗначениеИзСтрокиВнутр("{""#"",51e7a0d2-530b-11d4-b98a-008048da3034,{0,{""S"",""" + СтрЗаменить(СтрЗаменить(Строка, """", """"""), Разделитель, """},{""S"",""") + """}}}");
КонецФункции 

Функция ТзВМассивСтруктур(тз) Экспорт 
    Массив = Новый Массив;
	Для каждого Строка Из тз Цикл
		Структура = Новый  Структура;
		Для Каждого Колонка Из тз.Колонки Цикл
			Структура.Вставить(Колонка.Имя,Строка[Колонка.Имя]);
		КонецЦикла;
        Массив.Добавить(Структура);
    КонецЦикла;
    Возврат Массив;
КонецФункции

Функция ТзВСтруктуруМассивов(тз) Экспорт 
	Структура = Новый  Структура;
	Для Каждого Колонка Из тз.Колонки Цикл
		Массив = Новый Массив;
		Для Каждого Строка Из тз Цикл
			Массив.Добавить(Строка[Колонка.Имя]);
		КонецЦикла;
		Структура.Вставить(Колонка.Имя,Массив);
	КонецЦикла;
	Возврат Структура;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСсылкуСправочникАлгоритмы(Алгоритм) Экспорт
    Если ТипЗнч(Алгоритм) = Тип("СправочникСсылка._37583_ALG") Тогда 
        Возврат Алгоритм;
    ИначеЕсли ТипЗнч(Алгоритм) = Тип("УникальныйИдентификатор") Тогда
        Возврат Справочники._37583_ALG.ПолучитьСсылку(Алгоритм);
    ИначеЕсли ТипЗнч(Алгоритм) = Тип("Строка") Тогда 
        Если Лев(Алгоритм,5) = "GUID_" Тогда // БСП внеш. обработка
            СтрокаУИД = Сред(Алгоритм,6);
            ref = Справочники._37583_ALG.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаУИД));
            Возврат ?(ПустаяСтрока(ref.Наименование),Неопределено,ref);
        КонецЕсли;
        НайденПоНаименованию = Справочники._37583_ALG.НайтиПоНаименованию(Алгоритм,Истина);
        Если НайденПоНаименованию = Неопределено Тогда
            Попытка
                СтрокаКод = Строка(Прав(Алгоритм,5));
                НайденПоКоду = Справочники._37583_ALG.НайтиПоКоду(СтрокаКод);
                Если НайденПоКоду = Неопределено Тогда
                    Возврат Неопределено;
                Иначе 
                    Возврат НайденПоКоду;
                КонецЕсли;	
            Исключение
                Возврат Неопределено;
            КонецПопытки;
        Иначе 
            Возврат НайденПоНаименованию;	
        КонецЕсли;	
    Иначе
        Возврат Неопределено;
    КонецЕсли;
КонецФункции

Функция ПолучитьКодАлгоритма(ПредставлениеАлгоритма) Экспорт
	
	Алгоритм = ПолучитьСсылкуСправочникАлгоритмы(ПредставлениеАлгоритма);
	
	Если Алгоритм = Неопределено Или Алгоритм.Пустая() Тогда
		ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер","Алгоритм = Неопределено Или Алгоритм.Пустая()" ,"ПолучитьКодАлгоритма");
	Иначе
		Возврат Алгоритм.КодАлгоритма;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьПараметр(ПредставлениеАлгоритма,НаименованиеПараметра) Экспорт
	Алгоритм = ПолучитьСсылкуСправочникАлгоритмы(ПредставлениеАлгоритма);
	Если Алгоритм = Неопределено Или Алгоритм.Пустая() Тогда
		Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер"," Алгоритм = Неопределено Или Алгоритм.Пустая()" ,"ПолучитьПараметр");
	ИначеЕсли ПустаяСтрока(НаименованиеПараметра) Тогда 
		Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер"," ПустаяСтрока(НаименованиеПараметра)" ,"ПолучитьПараметр");
	Иначе
		Объект = Алгоритм.ПолучитьОбъект();
		Параметр = Объект.ПолучитьПараметр(НаименованиеПараметра);
		Если Параметр = Неопределено Тогда
			Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер"," Параметр = Неопределено " ,"ПолучитьПараметр");
		Иначе 
			СтруктураВозврат = Новый Структура;
			СтруктураВозврат.Вставить(НаименованиеПараметра,Параметр);
			СтруктураВозврат.Вставить("Отказ",Ложь);
			Возврат СтруктураВозврат;
		КонецЕсли;	
	КонецЕсли;
КонецФункции

Функция ПолучитьПараметры(ПредставлениеАлгоритма,НаКлиенте = Ложь) Экспорт
    Алгоритм = ПолучитьСсылкуСправочникАлгоритмы(ПредставлениеАлгоритма);
	Если Алгоритм = Неопределено Или Алгоритм.Пустая() Тогда
		Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер"," Алгоритм = Неопределено Или Алгоритм.Пустая() " ,"ПолучитьПараметры");
    Иначе
        Объект = Алгоритм.ПолучитьОбъект();
        Параметры = Объект.ПолучитьПараметры();
        Параметры.Вставить("Отказ",Ложь);
        Если НаКлиенте Тогда
            Для Каждого Параметр Из Параметры Цикл
                Если ТипЗнч(Параметр.Значение) = Тип("ТаблицаЗначений") Тогда 
                    Параметры.Вставить(Параметр.Ключ,_37583_АлгоритмыСервер.ТзВМассивСтруктур(Параметр.Значение));
                КонецЕсли;
            КонецЦикла;
            Возврат ПоместитьВоВременноеХранилище(Параметры,Новый УникальныйИдентификатор);	
        Иначе
            Возврат Параметры;	
        КонецЕсли;		
    КонецЕсли;
КонецФункции

Функция ПолучитьСтруктуруВыполненияНаКлиенте(ПредставлениеАлгоритма, Параметры, ПоместитьВоВременноеХранилище = Истина) Экспорт
	
	Алгоритм = ПолучитьСсылкуСправочникАлгоритмы(ПредставлениеАлгоритма);
	
	Если Алгоритм = Неопределено Или Алгоритм.Пустая() Тогда
		Возврат ЗарегистрироватьОшибкуИВыброситьИсключение("_37583_АлгоритмыСервер"," Алгоритм = Неопределено Или Алгоритм.Пустая() " ,"ПолучитьСтруктуруВыполненияНаКлиенте");
	КонецЕсли;
	
	СвойстваАлгоритма = _37583_ОбщегоНазначенияКлиентСервер.СвойстваДоступныеНаКлиентеИНаСервере();
	
	АлгоритмОбъект = Алгоритм.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(СвойстваАлгоритма, АлгоритмОбъект);
	
	Если Не Параметры.Свойство("СообщениеОбОшибке") Тогда
		Параметры.Вставить("СообщениеОбОшибке",""); 
	КонецЕсли;
	
	Если Не Параметры.Свойство("Отказ") Тогда
		Параметры.Вставить("Отказ",Ложь); 
	КонецЕсли;

	Если Не СвойстваАлгоритма.ТолькоТекст Тогда
		ХранимыеПараметры = АлгоритмОбъект.ПолучитьПараметры();
		Для Каждого ХранимыйПараметр Из ХранимыеПараметры Цикл
			Если Не Параметры.Свойство(ХранимыйПараметр.Ключ) Тогда 
                Если ТипЗнч(ХранимыйПараметр.Значение) = Тип("ТаблицаЗначений") Тогда 
                    Параметры.Вставить(ХранимыйПараметр.Ключ,_37583_АлгоритмыСервер.ТзВМассивСтруктур(ХранимыйПараметр.Значение));
				Иначе
					Параметры.Вставить(ХранимыйПараметр.Ключ,ХранимыйПараметр.Значение);
                КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Параметры.Вставить("Ссылка",СвойстваАлгоритма.Ссылка) ;  

	Параметры.Вставить("КодАлгоритма", СвойстваАлгоритма.КодАлгоритма);  
	
	СвойстваАлгоритма.Вставить("Параметры",Параметры);

	Если СвойстваАлгоритма.ЗаписыватьСобытиияВЖР  Тогда 
		ЗаписатьВЖурналРегистрации(" выполнение "," (&НаКлиенте) "+ СвойстваАлгоритма.Наименование,СвойстваАлгоритма.Ссылка, "Информация");
	КонецЕсли;
	
	Если ПоместитьВоВременноеХранилище Тогда 
		
		Возврат ПоместитьВоВременноеХранилище(СвойстваАлгоритма,Новый УникальныйИдентификатор);
		
	Иначе
		
		Возврат СвойстваАлгоритма;
		
	КонецЕсли;
	
КонецФункции

Процедура ПолучитьСтруктуруВыполненияКомандыНаКлиенте(ПараметрыПодключаемыхКоманд, АдресНастроек)Экспорт
	
	Если ПараметрыПодключаемыхКоманд.Свойство("АдресаКоманд_37583") = Ложь Тогда
		
		АдресаКоманд_37583 = Новый Соответствие;
		
		ТаблицаКоманд = ПолучитьИзВременногоХранилища(АдресНастроек);
		
		Для Каждого СтрокаТаблицы Из ТаблицаКоманд Цикл
			
			СвойстваАлгоритма = ПолучитьСтруктуруВыполненияНаКлиенте(СтрокаТаблицы.Идентификатор , Новый Структура , Ложь);
			
			АдресаКоманд_37583.Вставить(СтрокаТаблицы.ИмяВФорме,СвойстваАлгоритма);
			
		КонецЦикла;
		
		ПараметрыПодключаемыхКоманд.Вставить("АдресаКоманд_37583",АдресаКоманд_37583);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗарегистрироватьОшибкуИВыброситьИсключение(Модуль = "_37583_АлгоритмыСервер", ТекстОшибки, ИмяФункции)Экспорт
	
	ТекстИсключения = "37583_ Ошибка в модуле : " + Модуль + "." + ИмяФункции + "Текст ошибки :" + Символы.ПС + ТекстОшибки;
	
	ЗаписатьВЖурналРегистрации(ТекстИсключения , ТекстОшибки);
	
	ВызватьИсключение ТекстИсключения ;
	
	Возврат Неопределено;

КонецФункции

#КонецОбласти

